plugins {
	id 'groovy'
	id 'application'
}

mainClassName = 'org.strangeforest.tcb.dataload.Main'
applicationDefaultJvmArgs = [
	'-Xmx256m',
	'-Dtcb.db.url=jdbc:postgresql://localhost:5432/tcb',
	'-Dtcb.db.connections=1',
	'-Dtcb.data.base-dir=/tcb/tennis_atp'
]

sourceSets.main.resources.srcDirs = ['src/main/resources', 'src/main/db']

dependencies {
	implementation (project(':crystal-ball')) {
		exclude group: 'org.jetbrains.kotlin'
	}
	implementation (project(':tennis-stats')) {
		exclude group: 'org.springframework.boot'
		exclude group: 'io.micrometer'
		exclude group: 'org.thymeleaf.extras'
		exclude group: 'org.webjars'
		exclude group: 'org.webjars.bower'
		exclude group: 'com.maxmind.geoip2'
		exclude group: 'eu.bitwalker'
	}
	implementation ("org.codehaus.groovy:groovy:$groovyVersion")
	implementation ("org.codehaus.groovy:groovy-sql:$groovyVersion")
	implementation ("org.codehaus.groovy:groovy-xml:$groovyVersion")
	implementation ("org.codehaus.groovy:groovy-json:$groovyVersion")
	implementation ("org.codehaus.groovy:groovy-cli-commons:$groovyVersion")
	implementation ("org.codehaus.groovy:groovy-jmx:$groovyVersion")
	implementation ("com.xlson.groovycsv:groovycsv:$groovyCsvVersion")
	implementation ("org.jsoup:jsoup:$jsoupVersion")
	implementation ("org.springframework:spring-jdbc:$springVersion")
	implementation ("com.zaxxer:HikariCP:$hikariCPVersion")
	implementation ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
	implementation ("commons-cli:commons-cli:$commonsCliVersion")
	implementation ("org.slf4j:slf4j-simple:$slf4jVersion")
	testImplementation ("org.codehaus.groovy:groovy-test:$groovyVersion")
	testImplementation ("org.testcontainers:postgresql:$testContainersVersion")
}

test {
	exclude '**/*IT.class'
}

task quickDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def dataLoadJar = "data-load-${version}.jar"
		def crystalBallJar = "crystal-ball-${version}.jar"
		def tennisStatsJar = "tennis-stats-${version}.jar"
		def destPath = '/tcb/data-load/lib'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${buildDir}/libs/${dataLoadJar}", into: "${destPath}/${dataLoadJar}.new"
				put from: "${rootDir}/crystal-ball/build/libs/${crystalBallJar}", into: "${destPath}/${crystalBallJar}.new"
				put from: "${rootDir}/tennis-stats/build/libs/${tennisStatsJar}", into: "${destPath}/${tennisStatsJar}.new"
				execute "mv -f ${destPath}/${dataLoadJar}.new ${destPath}/${dataLoadJar}"
				execute "mv -f ${destPath}/${crystalBallJar}.new ${destPath}/${crystalBallJar}"
				execute "mv -f ${destPath}/${tennisStatsJar}.new ${destPath}/${tennisStatsJar}"
			}
		}
	}
}

task fullDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def dataLoad = "data-load-${version}"
		def dataLoadDist = "${dataLoad}.tar"
		def destPath = '/tcb'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${buildDir}/distributions/${dataLoadDist}", into: destPath
				execute "rm -rf ${destPath}/data-load"
				execute "cd ${destPath}; tar -xvf ${dataLoadDist}; rm ${dataLoadDist}; mv ${dataLoad} data-load"
			}
		}
	}
}