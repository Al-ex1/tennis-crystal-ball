buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsVersion"
		classpath "org.hidetake:gradle-ssh-plugin:$gradleSshVersion"
		classpath "org.ajoberstar:gradle-git:$gradleGitVersion"
		classpath "org.standardout:gradle-versioneye-plugin:$gradleVersionEyeVersion"
	}
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.standardout.versioneye'

allprojects {
	version = "$projectVersion"

	buildscript {
		repositories {
			mavenCentral()
		}
	}

	repositories {
		mavenCentral()
	}

	apply plugin: 'org.hidetake.ssh'

	remotes {
		def home = System.properties['user.home']
		openShift {
			host = 'tennis-strangeforest.rhcloud.com'
			user = '5637dc3a0c1e6656ae0001b2'
			identity = file("${home}/.ssh/id_rsa")
		}
		openShiftDB {
			host = 'tennisdb-strangeforest.rhcloud.com'
			user = '58f8ee0b2d527165b100008b'
			identity = file("${home}/.ssh/id_rsa")
		}
		digitalOcean {
			host = '46.101.166.59'
			user = 'root'
			identity = file("${home}/.ssh/id_rsa")
		}
	}
}

ext {
	git = org.ajoberstar.grgit.Grgit.open()
	releaseVersion = releaseVersion version
}

task wrapper(type: Wrapper) {
	gradleVersion = "$gradleVersion"
}

def releaseVersion(version) {
	def SNAPSHOT_SUFFIX = '-SNAPSHOT'
	version.endsWith(SNAPSHOT_SUFFIX) ? version.substring(0, version.length() - SNAPSHOT_SUFFIX.length()) : version
}

versioneye {
	includeSubProjects = true
	dependencies = transitive
}