import org.apache.tools.ant.filters.*

import groovy.json.*

plugins {
	id 'java'
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'application'
	id 'com.gorylenko.gradle-git-properties'
}

mainClassName = 'org.strangeforest.tcb.stats.TennisStatsApplication'
applicationDefaultJvmArgs = [
	'-Dspring.profiles.active=digitalocean,jobs,ssl',
	'-Xms256m',
	'-Xmx256m',
	'-Dcom.sun.management.jmxremote',
	'-Dcom.sun.management.jmxremote.port=9999',
	'-Dcom.sun.management.jmxremote.rmi.port=9999',
	'-Dcom.sun.management.jmxremote.authenticate=false',
	'-Dcom.sun.management.jmxremote.ssl=false',
	'--add-opens=java.base/java.time=ALL-UNNAMED',
	'--add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED',
	'-Xlog:gc=info:file=/tcb/tennis-stats/logs/gc.log:time,tags:filecount=10,filesize=10m',
	'-XX:+ExplicitGCInvokesConcurrent',
	'-XX:+HeapDumpOnOutOfMemoryError'
]

sourceSets {
	itest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	itestImplementation.extendsFrom testImplementation
	itestRuntimeOnly.extendsFrom testRuntimeOnly
}

processResources {
	filesMatching('application.properties') {
		filter ReplaceTokens, tokens: [
			'info.app.version': project.version.toString()
		]
	}
}

dependencies {
	implementation ('org.springframework.boot:spring-boot-starter-aop') {
		exclude group: 'org.apache.logging.log4j'
	}
	implementation ('org.springframework.boot:spring-boot-starter-jdbc') {
		exclude group: 'org.apache.logging.log4j'
	}
	implementation ('org.springframework.boot:spring-boot-starter-cache') {
		exclude group: 'org.apache.logging.log4j'
	}
	implementation ('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.apache.logging.log4j'
		exclude group: 'org.hibernate.validator'
		exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
		exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-parameter-names'
	}
	implementation ('org.springframework.boot:spring-boot-starter-thymeleaf') {
		exclude group: 'org.apache.logging.log4j'
	}
	implementation ('org.springframework.boot:spring-boot-starter-actuator') {
		exclude group: 'org.apache.logging.log4j'
	}
	compileOnly ('org.springframework.boot:spring-boot-configuration-processor')
	annotationProcessor ("org.springframework:spring-context-indexer:$springVersion")
	implementation ("com.github.ben-manes.caffeine:caffeine:$caffeineVersion") {
		exclude group: 'com.google.errorprone'
		exclude group: 'org.checkerframework'
	}
	implementation ("com.zaxxer:HikariCP:$hikariCPVersion")
	implementation ("com.neovisionaries:nv-i18n:$nvi18nVersion")
	implementation ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
	implementation ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
	implementation ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
	runtimeOnly ("org.webjars:bootstrap:$bootstrapVersion")
	runtimeOnly ("org.webjars:jquery:$jQueryVersion")
	runtimeOnly ("org.webjars:jquery-ui:$jQueryUIVersion")
	runtimeOnly ("org.webjars:jquery-ui-themes:$jQueryUIVersion")
	runtimeOnly ("org.webjars.bower:jquery-bootgrid:$jQueryBootgridVersion") {
		exclude group: 'org.webjars.bower'
	}
	runtimeOnly ("org.webjars:font-awesome:$fontAwesomeVersion")
	implementation ("com.maxmind.geoip2:geoip2:$geoip2Version") {
		exclude group: 'org.apache.httpcomponents'
	}
	runtimeOnly files ('lib/GeoLite2-Country.zip')
	implementation ("eu.bitwalker:UserAgentUtils:$userAgentUtilsVersion")
	runtimeOnly ("io.micrometer:micrometer-registry-prometheus:$micrometerVersion")

	testImplementation ('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.apache.logging.log4j'
	}
	testImplementation ("org.mockito:mockito-junit-jupiter:$mockitoVersion")
}

jar {
	enabled = true
}

bootJar {
	enabled = false
}

bootDistZip {
	enabled = false
}

bootDistTar {
	enabled = false
}

task quickDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def tennisStatsJar = "tennis-stats-${version}.jar"
		def destPath = '/tcb/tennis-stats/lib'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${buildDir}/libs/${tennisStatsJar}", into: "${destPath}/${tennisStatsJar}.new"
				execute 'systemctl stop tennis-stats.service'
				execute "mv -f ${destPath}/${tennisStatsJar}.new ${destPath}/${tennisStatsJar}"
				execute 'systemctl start tennis-stats.service'
			}
		}
	}
}

task fullDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def tennisStats = "tennis-stats-${version}"
		def tennisStatsDist = "${tennisStats}.tar"
		def destPath = '/tcb'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${buildDir}/distributions/${tennisStatsDist}", into: destPath
				execute "cd ${destPath}; tar -xvf ${tennisStatsDist}; rm ${tennisStatsDist}"
				execute 'systemctl stop tennis-stats.service'
				execute "rm -rf ${destPath}/tennis-stats/bin"
				execute "rm -rf ${destPath}/tennis-stats/lib"
				execute "rm -rf ${destPath}/tennis-stats/logs/*"
				execute "rm -rf ${destPath}/tennis-stats/wrapper/logs/*"
				execute "cd ${destPath}; mv -f ${tennisStats}/* tennis-stats; rm -rf ${tennisStats}"
				execute 'systemctl start tennis-stats.service'
			}
		}
	}
}

task productionHealthCheck {
	doLast {
		def health = new JsonSlurper().parse(new URL('https://www.ultimatetennisstatistics.com/actuator/health'))
		println "Ultimate Tennis Statistics is ${health.status}"
	}
}

task productionInfo {
	doLast {
		def info = new JsonSlurper().parse(new URL('https://www.ultimatetennisstatistics.com/actuator/info'))
		println "Ultimate Tennis Statistics: ${info.app.version}"
		println "DB size: ${info.db.size}"
		println "Visitors: ${info.visitors}"
	}
}