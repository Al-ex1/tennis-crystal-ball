buildscript {
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
		classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementVersion"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'

mainClassName = 'org.strangeforest.tcb.stats.TennisStatsApplication'
applicationDefaultJvmArgs = [
	'-Xms256m',
	'-Xmx256m',
	'-Dspring.profiles.active=digitalocean,jobs',
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	"integration-test" { sourceSet ->
		['java', 'groovy', 'resources'].each {
			if (!sourceSet.hasProperty(it)) return
			sourceSet."$it".srcDir file("src/integration-test/${it}")
		}
	}
}

processResources {
	filesMatching('application.properties') {
		filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
			'info.app.version': (project.property('version') + '-' + git.head().abbreviatedId).toString()
		]
	}
}

dependencies {
	compile ('org.springframework.boot:spring-boot-starter-aop')
	compile ('org.springframework.boot:spring-boot-starter-jdbc')
	compile ('org.springframework.boot:spring-boot-starter-cache')
	compile ('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.hibernate'
	}
	compile ('org.springframework.boot:spring-boot-starter-thymeleaf') {
		exclude group: 'org.codehaus.groovy'
		exclude group: 'org.hibernate'
	}
	compile ('org.springframework.boot:spring-boot-starter-logging')
	compile ('org.springframework.boot:spring-boot-starter-actuator')
	compile ("com.google.guava:guava:$guavaVersion")
	compile ("com.github.ben-manes.caffeine:caffeine:$caffeineVersion")
	compile ("org.postgresql:postgresql:$postgresqlJdbcVersion")
	compile ("com.neovisionaries:nv-i18n:$nvi18nVersion")
	compile ("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
	compile ("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
	compile ("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
	compile ("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
	runtime ("org.webjars:bootstrap:$bootstrapVersion")
	runtime ("org.webjars:jquery:$jQueryVersion")
	runtime ("org.webjars:jquery-ui:$jQueryUIVersion")
	runtime ("org.webjars:jquery-ui-themes:$jQueryUIVersion")
	runtime ("org.webjars.bower:jquery-bootgrid:$jQueryBootgridVersion") {
		exclude group: 'org.webjars.bower'
	}
	compile ("com.maxmind.geoip2:geoip2:$geoip2Version") {
		exclude group: 'com.google.http-client'
	}
	runtime files ('lib/GeoLite2-Country.zip')
	compile ("eu.bitwalker:UserAgentUtils:$userAgentUtilsVersion")

	testCompile ('org.springframework.boot:spring-boot-starter-test')
	testCompile ("junit:junit:$junitVersion")
	testCompile ("org.assertj:assertj-core:$assertjCoreVersion")
	testCompile ("org.mockito:mockito-core:$mockitoVersion")
	testCompile ("org.testng:testng:$testngVersion")

	integrationTestCompile sourceSets.main.output
	integrationTestCompile sourceSets.test.output
	integrationTestCompile configurations.testCompile
	integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets."integration-test".output.classesDir
	classpath = sourceSets."integration-test".runtimeClasspath
}

bootRepackage {
	enabled = false
}

task quickDeployToOpenShift {
	doLast {
		def version = project.property('version')
		def tennisStatsJar = "tennis-stats-${version}.jar"
		def srcPath = 'build/libs'
		def destPath = "app-root/data/tennis-stats/lib"
		ssh.run {
			session(remotes.openShift) {
				put from: "${srcPath}/${tennisStatsJar}", into: "${destPath}/${tennisStatsJar}.new"
				execute 'gear stop'
				execute 'rm -rf app-root/logs/*'
				execute "mv -f ${destPath}/${tennisStatsJar}.new ${destPath}/${tennisStatsJar}"
				execute 'gear start'
			}
		}
	}
}

task fullDeployToOpenShift {
	doLast {
		def version = project.property('version')
		def tennisStats = "tennis-stats-${version}"
		def tennisStatsDist = "${tennisStats}.tar"
		def srcPath = 'build/distributions'
		def destPath = 'app-root/data'
		ssh.run {
			session(remotes.openShift) {
				put from: "${srcPath}/${tennisStatsDist}", into: destPath
				execute "cd ${destPath}; tar -xvf ${tennisStatsDist}; rm ${tennisStatsDist}"
				execute 'gear stop'
				execute "rm -rf ${destPath}/tennis-stats app-root/logs/*"
				execute "cd ${destPath}; mv ${tennisStats} tennis-stats"
				execute 'gear start'
			}
		}
	}
}

task quickDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def tennisStatsJar = "tennis-stats-${version}.jar"
		def srcPath = 'build/libs'
		def destPath = '/tcb/tennis-stats/lib'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${srcPath}/${tennisStatsJar}", into: "${destPath}/${tennisStatsJar}.new"
				execute '/tcb/stop-tennis-stats.sh'
				execute "mv -f ${destPath}/${tennisStatsJar}.new ${destPath}/${tennisStatsJar}"
				execute '/tcb/start-tennis-stats.sh'
			}
		}
	}
}

task fullDeployToDigitalOcean {
	doLast {
		def version = project.property('version')
		def tennisStats = "tennis-stats-${version}"
		def tennisStatsDist = "${tennisStats}.tar"
		def srcPath = 'build/distributions'
		def destPath = '/tcb'
		ssh.run {
			session(remotes.digitalOcean) {
				put from: "${srcPath}/${tennisStatsDist}", into: destPath
				execute "cd ${destPath}; tar -xvf ${tennisStatsDist}; rm ${tennisStatsDist}"
				execute '/tcb/stop-tennis-stats.sh'
				execute "rm -rf ${destPath}/tennis-stats"
				execute "cd ${destPath}; mv ${tennisStats} tennis-stats"
				execute '/tcb/start-tennis-stats.sh'
			}
		}
	}
}

task productionHealthCheck {
	doLast {
		def health = new groovy.json.JsonSlurper().parse(new URL('http://www.ultimatetennisstatistics.com/manage/health'))
		println "Ultimate Tennis Statistics is ${health.status}"
		println "Disk usage: ${health.quota.blocks.pctUsed}"
	}
}

task productionInfo {
	doLast {
		def info = new groovy.json.JsonSlurper().parse(new URL('http://www.ultimatetennisstatistics.com/manage/info'))
		println "Ultimate Tennis Statistics: ${info.app.version}"
		println "DB size: ${info.db.size}"
		println "Visitors: ${info.visitors}"
	}
}